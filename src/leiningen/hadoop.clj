(ns leiningen.hadoop
  "Create a jar for submission as a hadoop job."
  (:use
   [clojure.java.io   :only [file]]
   [leiningen.deps    :only [deps]]
   [leiningen.javac   :only [javac]])
  (:require
   [leiningen.compile :as compile]
   [leiningen.jar     :as jar]))

;; depends on private API :'(
(def filespecs #'jar/filespecs)

(defn add-lib-filespecs
  "Add :library-path to the list of filespecs. To avoid inclusion of lib/dev,
use in conjunction with exclude-lib-dev."
  [filespecs project]
  (concat
   (when (and (:library-path project)
              (.exists (file (:library-path project))))
     [{:type :path
       :path (:library-path project)}])
   filespecs))

(defn exclude-lib-dev
  "Add (:library-path project)/dev.*.jar to :jar-exclusions"
  [project]
  (let [lib-base (-> (:library-path project)
                     (.replace (str (:root project) "/") ""))
        dev-exclusions {:jar-exclusions
                        [(re-pattern (str lib-base "/dev/.*$"))]}]
    (merge-with concat project dev-exclusions)))

(defn hadoopjar-name
  "Determine a file name for the hadoop jar. Respects name definitions from
project in order: :hadoop-name, :jar-name"
  [project]
  (or (:hadoop-name project)
      (-> (jar/get-default-jar-name project)
          (.replaceAll ".jar$" "-hadoop.jar"))))

(defn ^{:help-arglists '([])} hadoop
  "Create a jar for submission to Hadoop.

Create a $PROJECT-$VERSION-hadoop.jar file. This file is identical to the one
generated by jar, except it includes all project dependencies in the jar
under lib/. The contents of lib/dev are excluded. Filename can be overridden
via either :hadoop-name or :jar-name in project.clj."
  [project]
  (let [deps-fileset (deps project)       ;; fetch deps
        _ (javac project)                 ;; compile java
        status (compile/compile project)] ;; compile clj
    (if (zero? status)
      (let [jar-path (jar/get-jar-filename project (hadoopjar-name project))
            specs (add-lib-filespecs (filespecs project deps-fileset) project)]
        (jar/write-jar (exclude-lib-dev project) jar-path specs)
        (println "Created" jar-path)
        jar-path)
      status)))
